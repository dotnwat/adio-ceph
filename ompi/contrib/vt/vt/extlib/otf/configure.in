#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.


AC_PREREQ(2.57)
AC_INIT(OTF, 1.3.12, andreas.knuepfer@tu-dresden.de )
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(OTF, 1.3.12)
AC_CONFIG_SRCDIR([config.h.in])
AM_CONFIG_HEADER(config.h)

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_DISABLE_SHARED
AC_PROG_LIBTOOL

# Own stuff

# Do we want to build and install binaries?
AC_ARG_ENABLE(binaries,
	AC_HELP_STRING([--enable-binaries],
		[build and install binaries, default: enabled]),
	[enable_binaries="$enableval"], [enable_binaries="yes"])
AM_CONDITIONAL(AMBUILDBINARIES, test x"$enable_binaries" = "xyes")

# Checks for VTF3.
CHECK_VTF3
if test x"$force_vtf3" = "xyes" -a x"$vtf3_error" = "xyes"; then exit 1; fi
AM_CONDITIONAL(AMHAVEVTF3, test x"$have_vtf3" = xyes)

# Checks for zlib compression.
CHECK_ZLIB
if test x"$force_zlib" = "xyes" -a x"$zlib_error" = "xyes"; then exit 1; fi
AM_CONDITIONAL(AMHAVEZLIB, test x"$have_zlib" = xyes)
AM_CONDITIONAL(AMDEPZLIB, test x"$have_zlib" = xyes -a x"$enable_shared" = xyes)

WITH_DEBUG
WITH_VERBOSE


# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stddef.h stdlib.h string.h sys/time.h unistd.h])
AC_CHECK_HEADER([stdint.h], [have_stdint_h=1], [have_stdint_h=0])
AC_CHECK_HEADER([inttypes.h], [have_inttypes_h=1], [have_inttypes_h=0])
AC_SUBST([have_stdint_h])
AC_SUBST([have_inttypes_h])

# Checks for library functions.
AC_CHECK_DECL([va_copy],
[AC_DEFINE([HAVE_VA_COPY], [1], [Define to 1 if you have the `va_copy' function.])],
[], [#include <stdarg.h>])
AC_CHECK_DECL([__va_copy],
[AC_DEFINE([HAVE_UNDERSCORE_VA_COPY], [1], [Define to 1 if you have the `__va_copy' function.])],
[], [#include <stdarg.h>])
AC_CHECK_FUNCS([asprintf snprintf vasprintf vsnprintf strdup fseeko ftello])

# Checks for typedefs, structures, and compiler characteristics.
# AC_C_CONST ### do not use this if you want to do C++ programs because it might define 'const'
#	to an empty string which produces a total mess later on!!!

AC_TYPE_SIZE_T
#AC_HEADER_TIME

# Checks for library functions.
# AC_FUNC_FSEEKO
#AC_FUNC_MALLOC
#AC_FUNC_REALLOC
#AC_FUNC_STAT
#AC_CHECK_FUNCS([gettimeofday memmove strdup])

# Checks for datatype sizes.
AC_CHECK_SIZEOF(long, 8)
sizeof_long=$ac_cv_sizeof_long
AC_SUBST([sizeof_long])

AC_CONFIG_FILES([Makefile
	otflib/Makefile
	otflib/OTF_inttypes_unix.h
	tools/Makefile
	tools/otfaux/Makefile
	tools/otfcompress/Makefile
	tools/otfconfig/Makefile
	tools/otfdump/Makefile
	tools/otfinfo/Makefile
	tools/otfmerge/Makefile
	tools/otf2vtf/Makefile
	tools/vtf2otf/Makefile
	docu/Makefile
])

## workaround for stupid warning caused by newer autoconf versions
AC_DEFUN([AC_DATAROOTDIR_CHECKED])

AC_OUTPUT
