# -*- shell-script -*-
#
# Copyright (c) 2004-2005 The Trustees of Indiana University.
#                         All rights reserved.
# Copyright (c) 2004-2005 The Trustees of the University of Tennessee.
#                         All rights reserved.
# Copyright (c) 2004-2005 High Performance Computing Center Stuttgart, 
#                         University of Stuttgart.  All rights reserved.
# $COPYRIGHT$
# 
# Additional copyrights may follow
# 
# $HEADER$
#

# Because of the following statements, this component is essentially
# "core" and cannot be configured / compiled outside of the Open MPI
# tree.

sinclude(../../../../config/ompi_config_subdir.m4)


#
# Main function.  This will be invoked in the middle of the templated
# configure script.
#
AC_DEFUN([MCA_CONFIGURE_STUB],[
    AC_ARG_ENABLE([mpi-profile],
        AC_HELP_STRING([--disable-mpi-profile],
                [Disable MPI profiling, and therefore the ROMIO io component]))

    AC_MSG_CHECKING([if MPI profiling is enabled])
    if test "$enable_mpi_profile" = "no"; then
        AC_MSG_RESULT([no])
        AC_MSG_WARN([*** The ROMIO io component requires the MPI profiling layer])
        AC_MSG_ERROR([Cannot continue])
    fi
    AC_MSG_RESULT([yes])

    AC_ARG_WITH([io-romio-flags], 
        AC_HELP_STRING([--with-io-romio-flags=FLAGS],
                [Pass FLAGS to the ROMIO distribution configuration script]))

    AC_MSG_CHECKING([for ROMIO distribution configure flags])
    io_romio_flags=
    if test -n "$with_io_romio_flags" -a "$with_io_romio_flags" != "no"; then
        io_romio_flags="$with_io_romio_flags $io_romio_flags"
    fi

    shared=enable
    if test -n "$enable_shared"; then
        if test "$enable_shared" != "no"; then
            shared=enable
        else
            shared=disable
        fi
    fi

    static=enable
    if test -n "$enable_static"; then
        if test "$enable_static" != "no"; then
            static=enable
        else
            static=disable
        fi
    fi

    prefix_arg=
    if test -n "$prefix" -a "$prefix" != "NONE"; then
        prefix_arg="--prefix=$prefix"
    fi

    io_romio_flags="CFLAGS="'"'"$CFLAGS"'"'" CPPFLAGS="'"'"$CPPFLAGS"'"'" FFLAGS="'"'"$FFLAGS"'"'" LDFLAGS="'"'"$LSFLAGS"'"'" --$shared-shared --$static-static $io_romio_flags $prefix_arg"
    AC_MSG_RESULT([$io_romio_flags])

    ompi_show_subtitle "Configuring ROMIO distribution"
    OMPI_CONFIG_SUBDIR([romio-dist], [$io_romio_flags],
                        [HAPPY=1], [HAPPY=0])
    if test "$HAPPY" = "1"; then
        echo "ROMIO distribution configured successfully"
    else
        AC_MSG_WARN([ROMIO distribution did not configure successfully])
        AC_MSG_ERROR([Cannot continue])
    fi
])

#
# For dist
#
AC_DEFUN([MCA_CONFIGURE_DIST_STUB],[
    ompi_show_subtitle "Configuring ROMIO distribution (dist only)"
    OMPI_CONFIG_SUBDIR([romio-dist], [],
                        [HAPPY=1], [HAPPY=0])
    if test "$HAPPY" = "1"; then
        echo "ROMIO distribution configured successfully (dist only)"
    else
        AC_MSG_WARN([ROMIO distribution did not configure successfully])
        AC_MSG_WARN([Nothing I can do about that -- sorry!])
        AC_MSG_ERROR([Cannot continue])
    fi
])
