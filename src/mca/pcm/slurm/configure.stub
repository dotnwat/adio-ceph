# -*- shell-script -*-
#
# Copyright (c) 2004-2005 The Trustees of Indiana University.
#                         All rights reserved.
# Copyright (c) 2004-2005 The Trustees of the University of Tennessee.
#                         All rights reserved.
# $COPYRIGHT$
# 
# Additional copyrights may follow
# 
# $HEADER$
#

#
# Main function.  This will be invoked in the middle of the templated
# configure script.
#
AC_DEFUN([MCA_CONFIGURE_STUB],[
#
# Make a best effort to see if we are on a SLURM system.  Also allow
# user to specify location to the slurm prefix, which will work just as
# well :).
#

OMPI_HAVE_SLURM=0
SLURM_LDFLAGS=""

#
AC_ARG_WITH(slurm, 
    AC_HELP_STRING([--with-slurm=DIR],
		   [directory where the slurm software was installed]))


#
# Case 1: --without-slurm specified - overrides autodetect
#
if test "$with_slurm" = "no"; then
    AC_MSG_ERROR([*** SLURM explicitly disabled])

#
# Case 2: --with-slurm specified - look in generic places for slurm libs
#
elif test "$with_slurm" = "yes"; then
    # See if we can find the slurm libraries...
    LIBS_save="$LIBS"
    AC_CHECK_LIB(slurm, slurm_api_version,
	   OMPI_HAVE_SLURM=1,
	   AC_MSG_ERROR([*** Connot find working libslurm.]))
    LIBS="$LIBS_save"

#
# Case 3: --with-slurm=<foo> specified - try where they said to find it
#
else
    SLURM_DIR=$with_slurm

    if test -n "$SLURM_DIR"; then
	# Make the tests work...
	OLDLDFLAGS="$LDFLAGS"
	OLDCPPFLAGS="$CPPFLAGS"
	SLURM_LDFLAGS="-L$SLURM_DIR/lib"
	LDFLAGS="$LDFLAGS $SLURM_LDFLAGS"
	CPPFLAGS="$CPPFLAGS -I$SLURM_DIR/include"
	LIBS_save="$LIBS"

	AC_CHECK_LIB(slurm, slurm_api_version, OMPI_HAVE_SLURM=1,
		AC_MSG_ERROR([*** Cannot find working libslurm.]))

	# Since we are going to add the -L and -l to LIBOMPI_EXTRA_LIBS,
	# we reset this to the start ...
	LDFLAGS="$OLDLDFLAGS"
	CPPFLAGS="$OLDCPPFLAGS"
	LIBS="$LIBS_save"

    else	
	AC_CHECK_LIB(slurm, slurm_api_version, OMPI_HAVE_SLURM=1,
	    AC_MSG_ERROR([*** Cannot find working libslurm.]))
    fi
fi

AC_MSG_CHECKING([if want SLURM support])

if test "$OMPI_HAVE_SLURM" = "1"; then

    AC_MSG_RESULT([yes])
#
# Ok, we have slurm support.  Add proper things to the various
# compiler flags..
#
    LIBMPI_EXTRA_LDFLAGS="$SLURM_LDFLAGS"
    LIBMPI_EXTRA_LIBS="-lslurm"

    WRAPPER_EXTRA_LDFLAGS="$SLURM_LDFLAGS"
    WRAPPER_EXTRA_LIBS="-lslurm"

    LDFLAGS="$LDFLAGS $SLURM_LDFLAGS"
    LIBS="$LIBS -lslurm"

else
    AC_MSG_RESULT([no])
fi


AC_DEFINE_UNQUOTED(OMPI_HAVE_SLURM, $OMPI_HAVE_SLURM, 
    [Whether we have slurm support or not])

# Clean up
unset SLURM_LDFLAGS
])dnl
