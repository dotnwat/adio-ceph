# -*- shell-script -*-
#
# Copyright (c) 2004-2005 The Trustees of Indiana University.
#                         All rights reserved.
# Copyright (c) 2004-2005 The Trustees of the University of Tennessee.
#                         All rights reserved.
# $COPYRIGHT$
# 
# Additional copyrights may follow
# 
# $HEADER$
#

#
# Main function.  This will be invoked in the middle of the templated
# configure script.
#
AC_DEFUN([MCA_CONFIGURE_STUB],[
#
# Make a best effort to see if we are on a RMS system.  Also allow
# user to specify location to the rms prefix, which will work just as
# well :).
#

OMPI_HAVE_RMS=0
RMS_LDFLAGS=""

#
AC_ARG_WITH(rms, 
    AC_HELP_STRING([--with-rms=DIR],
		   [directory where the rms software was installed]))


#
# Case 1: --without-rms specified - overrides autodetect
#
if test "$with_rms" = "no"; then
    AC_MSG_ERROR([*** RMS explicitly disabled])

#
# Case 2: --with-rms specified - look in generic places for rms libs
#
elif test "$with_rms" = "yes"; then
    # See if we can find the rms libraries...
    LIBS_save="$LIBS"
    AC_CHECK_LIB(rmsapi, rms_numCpus,
	   OMPI_HAVE_RMS=1,
	   AC_MSG_ERROR([*** Connot find working librmsapi.]),[-lrms])
    LIBS="$LIBS_save"

#
# Case 3: --with-rms=<foo> specified - try where they said to find it
#
else
    RMS_DIR=$with_rms

    if test -n "$RMS_DIR"; then
	# Make the tests work...
	OLDLDFLAGS="$LDFLAGS"
	OLDCPPFLAGS="$CPPFLAGS"
	RMS_LDFLAGS="-L$RMS_DIR/lib"
	LDFLAGS="$LDFLAGS $RMS_LDFLAGS"
	CPPFLAGS="$CPPFLAGS -I$RMS_DIR/include"
	LIBS_save="$LIBS"

	AC_CHECK_LIB(rmsapi, rms_numCpus, OMPI_HAVE_RMS=1,
		AC_MSG_ERROR([*** Cannot find working librmsapi.]), [-lrms])

	# Since we are going to add the -L and -l to LIBOMPI_EXTRA_LIBS,
	# we reset this to the start ...
	LDFLAGS="$OLDLDFLAGS"
	CPPFLAGS="$OLDCPPFLAGS"
	LIBS="$LIBS_save"

    else	
	AC_CHECK_LIB(rmsapi, rms_numCpus, OMPI_HAVE_RMS=1,
	    AC_MSG_ERROR([*** Cannot find working librmsapi.]), [-lrms])
    fi
fi

AC_MSG_CHECKING([if want RMS support])

if test "$OMPI_HAVE_RMS" = "1"; then

    AC_MSG_RESULT([yes])
#
# Ok, we have rms support.  Add proper things to the various
# compiler flags..
#
    LIBMPI_EXTRA_LDFLAGS="$RMS_LDFLAGS"
    LIBMPI_EXTRA_LIBS="-lrms -lrmsapi"

    WRAPPER_EXTRA_LDFLAGS="$RMS_LDFLAGS"
    WRAPPER_EXTRA_LIBS="-lrms -lrmsapi"

    LDFLAGS="$LDFLAGS $RMS_LDFLAGS"
    LIBS="$LIBS -lrms -lrmsapi"

else
    AC_MSG_RESULT([no])
fi


AC_DEFINE_UNQUOTED(OMPI_HAVE_RMS, $OMPI_HAVE_RMS, 
    [Whether we have rms support or not])

# Clean up
unset RMS_LDFLAGS
])dnl
